plugins {
    id("java")
    id("org.jetbrains.intellij") version "1.17.0" // 或更高稳定版本
    id("maven-publish")
    id("distribution")  // 加上 distribution 插件
}

group = "tech.shiker"
version = "1.0.7"

repositories {
    mavenCentral()
}

// Configure Gradle IntelliJ Plugin
// Read more: https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin.html
intellij {
    version = '2023.3'
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.vladsch.flexmark:flexmark-all:0.64.8'
}


//新增java编译配置
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation', '-parameters']
}

// 添加一个任务用于输出当前版本号
tasks.register("printVersion") {
    doLast {
        println project.version.toString()
    }
}

// 配置distribution，打包成带版本号的zip
distributions {
    main {
        distributionBaseName.set("orange-assistant")
    }
}

patchPluginXml {
    //注意这个版本号不能高于上面intellij的version,否则runIde会报错
    sinceBuild = '231'
    untilBuild = '252.*'
    changeNotes = "哈哈，我又对AI能力进行了强化，你试试看！"
}



publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/shiker1996/orange-assistant")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact(tasks.distZip) {
                extension = "zip"
            }
        }
    }
}